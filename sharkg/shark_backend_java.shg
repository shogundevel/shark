###############################################################################
### Copyright #################################################################
## 
## Copyright 2022 Daniel Alvarez <shogundevel@gmail.com>
## 
## Permission is hereby granted, free of charge, to any person
## obtaining a copy of this software and associated documentation files
## (the "Software"), to deal in the Software without restriction,
## including without limitation the rights to use, copy, modify, merge,
## publish, distribute, sublicense, and/or sell copies of the Software,
## and to permit persons to whom the Software is furnished to do so,
## subject to the following conditions:
## 
## The above copyright notice and this permission notice shall be
## included in all copies or substantial portions of the Software.
## 
## THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
## EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
## MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
## IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
## CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
## TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
## SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
## 
###############################################################################

import "shark_backend.api"

class shark_backend_java (shark_backend)
    var indent_level: int
    var path: str
    var out: FILE
    var buf: strbuf
    var function_context: shark_function
    var init: bool
    var cond: bool
    var loop_name: str
    var exp: str
    var exp_left: str
    var exp_base: str
    var call_stack: list
    var call_args: list
    
    function shark_backend_java()
    function enter_source(name: str, type: bool)
    function exit_source()
    function indent()
    function dedent()
    function write_indent()
    function write_type(type: shark_type)
    function push()
    function exit_assign()
    function write_args()
    function exit_call()

class shark_backend_java
    function shark_backend_java()
        self.indent_level = 0
        self.path = null
        self.out = null
        self.buf = null
        self.function_context = null
        self.init = false
        self.cond = false
        self.exp = null
        self.exp_left = null
        self.exp_base = null
        self.call_stack = null
        self.call_args = null
    
    function enter_source(name: str, type: bool)
        if not type then
            self.out.write("public static class ")
            self.out.write(name)
            self.out.write("\n{\n")
    
    function exit_source()
        self.out.write("}\n\n")
    
    function indent()
        self.indent_level += 1
    
    function dedent()
        self.indent_level -= 1
    
    function write_indent()
        for i to self.indent_level do
            self.out.write("    ")
        
    function begin(out: str)
        self.buf = new strbuf ()
        self.path = out
        self.out = open(out, 'w')
        
        if self.out == null then
            write("can't open output file '")
            write(out)
            write("' for writing, compilation aborted.")
            exit(EXIT_FAILURE)
        
        self.call_stack = new list ()
    
    function close()
        self.out.close()
    
    function write_type(type: shark_type)
        if instanceof(type, shark_basic_type) then
            if type == shark_basic_type_bool then
                self.buf.write("boolean")
            else if type == shark_basic_type_int then
                self.buf.write("long")
            else if type == shark_basic_type_float then
                self.buf.write("double")
            else if type == shark_basic_type_char then
                self.buf.write("char")
            else if type == shark_basic_type_str then
                self.buf.write("String")
            else
                self.buf.write("Object")
        else if instanceof(type, shark_custom_type) then
            self.buf.write(cast(type, shark_custom_type).name)
        else
            var child: shark_type = cast(type, shark_array_type).type
            self.write_type(child)
            self.buf.write("[]")
        
    function enter_type_decl(type: shark_custom_type)
        shark_null_stat()
    
    function exit_type_decl()
        shark_null_stat()
    
    function enter_type_impl(type: shark_custom_type)
        self.enter_source(type.name, true)
        self.out.write("public static class ")
        self.out.write(type.name)
        
        if type.parent != null then
            self.out.write(" extends ")
            self.out.write(type.parent.name)
            
        self.out.write("\n{\n")
        self.indent()
        
        for i to type.fields.size() do
            var field: shark_variable = cast(type.fields.get(i), shark_variable)
            self.write_indent()
            self.out.write("public ")
            self.write_type(field.type)
            self.out.write(self.buf.get())
            self.out.write(" ")
            self.out.write(field.name)
            self.out.write(";\n")
        
    function exit_type_impl()
        self.dedent()
        self.exit_source()
    
    function var_decl(name: str, type: shark_type)
        self.write_indent()
        self.write_type(type)
        self.out.write(self.buf.get())
        self.out.write(" ")
        self.out.write(name)
        self.out.write(" = ")
        self.out.write(self.exp)
        self.out.write(";\n")
    
    function global_var_decl(name: str, type: shark_type, value: bool)
        if value then
            self.indent()
            self.write_indent()
            self.dedent()
            self.out.write("public static ")
            self.write_type(type)
            self.out.write(self.buf.get())
            self.out.write(" ")
            self.out.write(name)
            self.out.write(" = ")
            self.out.write(self.exp)
            self.out.write(";\n")
    
    function declare_constructor(function_object: shark_function)
        shark_null_stat()
    
    function declare_method(function_object: shark_function)
        shark_null_stat()
    
    function declare_function(function_object: shark_function)
        shark_null_stat()
    
    function close_function_declaration()
        shark_null_stat()
    
    function enter_function(function_object: shark_function)
        if function_object.source_type != null then
            self.write_indent()
            self.out.write("public ")
            if function_object.name == function_object.source_type.name then
                if function_object.arg_names.size() != 0 then
                    self.out.write(function_object.name)
                    self.out.write("() { }\n")
                    self.out.write("public ")
                self.out.write(function_object.name)
            else
                if function_object.type != null then
                    self.write_type(function_object.type)
                    self.out.write(self.buf.get())
                else
                    self.out.write("void")
                self.out.write(" ")
                self.out.write(function_object.name)
        else
            self.indent()
            self.write_indent()
            self.out.write("public static ")
            if function_object.type != null then
                self.write_type(function_object.type)
                self.out.write(self.buf.get())
            else
                self.out.write("void")
            self.out.write(" ")
            self.out.write(function_object.name)
            self.function_context = function_object
        
        self.out.write("(")
        
        for i to function_object.arg_names.size() do
            if i != 0 then
                self.out.write(", ")
            self.write_type(cast(function_object.arg_types.get(i), shark_type))
            self.out.write(self.buf.get())
            self.out.write(" ")
            self.out.write(cast(function_object.arg_names.get(i), str))
        
        self.out.write(") {\n")
        
        if function_object.source_type != null and function_object.name == function_object.source_type.name then
            if function_object.source_type.constructor != null then
                self.indent()
                self.write_indent()
                self.dedent()
                self.out.write("super();\n")
    
    function exit_function()
        self.write_indent()
        self.out.write("}\n")
        if self.function_context != null then
            self.function_context = null
            self.dedent()
    
    function inline_stat(code: str)
        self.write_indent()
        self.out.write(code)
        self.out.write("\n")
    
    function enter_block()
        self.indent()
    
    function empty_block()
        shark_null_stat()
    
    function exit_block()
        self.dedent()
    
    function if_stat()
        self.write_indent()
        self.out.write("if (")
        self.out.write(self.exp)
        self.out.write(") {\n")
    
    function enter_else_if()
        shark_null_stat()
    
    function else_if()
        self.write_indent()
        self.out.write("} else if (")
        self.out.write(self.exp)
        self.out.write(") {\n")
    
    function else_clause()
        self.write_indent()
        self.out.write("} else {\n")
    
    function close_if()
        self.write_indent()
        self.out.write("}\n")
    
    function close_else_if()
        shark_null_stat()
    
    function select_stat()
        self.write_indent()
        self.out.write("switch (")
        self.out.write(self.exp)
        self.out.write(") {\n")
    
    function enter_select_case(value: str)
        self.write_indent()
        self.out.write("case ")
        self.out.write(value)
        self.out.write(": {\n")
        self.indent()
    
    function exit_select_case()
        self.write_indent()
        self.out.write("break;\n")
        self.dedent()
        self.write_indent()
        self.out.write("}\n")
    
    function enter_select_else()
        self.write_indent()
        self.out.write("default:\n")
    
    function exit_select()
        self.write_indent()
        self.out.write("break; }\n")
    
    function enter_while()
        shark_null_stat()
    
    function while_stat()
        self.write_indent()
        self.out.write("while (")
        self.out.write(self.exp)
        self.out.write(") {\n")
    
    function exit_while()
        self.write_indent()
        self.out.write("}\n")
    
    function enter_for()
        self.write_indent()
        self.out.write("for (")
        self.init = false
        self.cond = false
    
    function push()
        self.exp_base = self.exp_left
        self.exp_left = self.exp
    
    function for_init(name: str, type: shark_type)
        self.write_type(type)
        self.out.write(self.buf.get())
        self.out.write(" ")
        self.out.write(name)
        self.out.write(" = 0; ")
        self.loop_name = name
        self.init = true
    
    function for_cond()
        self.out.write(" ")
        self.out.write(self.loop_name)
        self.out.write(" < ")
        self.out.write(self.exp)
        self.out.write(";")
        self.init = true
        self.cond = true
    
    function for_stat()
        self.out.write(" ")
        self.out.write(self.loop_name)
        self.out.write("++) {\n")
    
    function exit_for()
        self.write_indent()
        self.out.write("}\n")
    
    function break_stat()
        self.write_indent()
        self.out.write("break;\n")
    
    function continue_stat()
        self.write_indent()
        self.out.write("continue;\n")
    
    function return_stat(value: bool)
        self.write_indent()
        if value then
            self.out.write("return ")
            self.out.write(self.exp)
            self.out.write(";\n")
        else
            self.out.write("return;\n")
        
    function assign_stat()
        self.write_indent()
        self.out.write(self.exp)
        self.out.write(";\n")
    
    function enter_assign()
        self.call_stack.push(self.exp_left)
        self.call_stack.push(self.exp_base)
        self.push()
    
    function exit_assign()
        self.exp_base = cast(self.call_stack.pop(), str)
        self.exp_left = cast(self.call_stack.pop(), str)
    
    function enter_binop()
        self.call_stack.push(self.exp_base)
        self.call_stack.push(self.exp_left)
        self.push()
    
    function binary_op(op: str, strop: bool, objop: bool)
        if not strop then
            if op == "and" then
                op = "&&"
            if op == "or" then
                op = "||"
            self.buf.write(self.exp_left)
            self.buf.write(op)
            self.buf.write(self.exp)
        else
            if op == "!=" then
                self.buf.write("!")
            self.buf.write(self.exp_left)
            self.buf.write(".equals(")
            self.buf.write(self.exp)
            self.buf.write(")")
        self.exp = self.buf.get()
        self.exp_left = cast(self.call_stack.pop(), str)
        self.exp_base = cast(self.call_stack.pop(), str)
    
    function unary_op(op: str)
        if op == "not" then
            op = "!"
        self.buf.write(op)
        self.buf.write(self.exp)
        self.exp = self.buf.get()
    
    function enter_index()
        self.call_stack.push(self.exp_base)
        self.call_stack.push(self.exp_left)
        self.push()
    
    function inc_index(op: str, type: shark_type)
        self.buf.write(self.exp_left)
        self.buf.write("[(int) (")
        self.buf.write(self.exp)
        self.buf.write(")]")
        self.buf.write(op)
        self.exp_left = cast(self.call_stack.pop(), str)
        self.exp_base = cast(self.call_stack.pop(), str)
        self.exp = self.buf.get()
    
    function set_index(op: str, type: shark_type)
        self.buf.write(self.exp_base)
        self.buf.write("[(int) (")
        self.buf.write(self.exp_left)
        self.buf.write(")]")
        self.buf.write(op)
        self.buf.write(self.exp)
        self.exp_left = cast(self.call_stack.pop(), str)
        self.exp_base = cast(self.call_stack.pop(), str)
        self.exp = self.buf.get()
        self.exit_assign()
    
    function get_index(type: shark_type)
        self.buf.write(self.exp_left)
        self.buf.write("[(int) (")
        self.buf.write(self.exp)
        self.buf.write(")]")
        self.exp_left = cast(self.call_stack.pop(), str)
        self.exp_base = cast(self.call_stack.pop(), str)
        self.exp = self.buf.get()
    
    function call_method(function_object: shark_function)
        self.buf.write(self.exp_left)
        self.buf.write(".")
        self.buf.write(function_object.name)
        self.buf.write("(")
        self.write_args()
        self.buf.write(")")
        self.exit_call()
        self.exp = self.buf.get()
    
    function write_args()
        for i to self.call_args.size() do
            if i != 0 then
                self.buf.write(", ")
            self.buf.write(cast(self.call_args.get(i), str))
    
    function enter_call()
        self.call_stack.push(self.exp_left)
        self.call_stack.push(self.exp_base)
        self.call_stack.push(self.call_args)
        self.push()
        self.call_args = new list ()
    
    function exit_call()
        self.call_args = cast(self.call_stack.pop(), list)
        self.exp_base = cast(self.call_stack.pop(), str)
        self.exp_left = cast(self.call_stack.pop(), str)
    
    function push_arg()
        self.call_args.push(self.exp)
    
    function inc_field(field: shark_variable, op: str)
        self.buf.write(self.exp)
        self.buf.write(".")
        self.buf.write(field.name)
        self.buf.write(op)
        self.exp = self.buf.get()
    
    function set_field(field: shark_variable, op: str)
        self.buf.write(self.exp_left)
        self.buf.write(".")
        self.buf.write(field.name)
        self.buf.write(op)
        self.buf.write(self.exp)
        self.exp = self.buf.get()
        self.exit_assign()
    
    function get_field(field: shark_variable)
        self.buf.write(self.exp)
        self.buf.write(".")
        self.buf.write(field.name)
        self.exp = self.buf.get()
    
    function inc_local(name: str, op: str)
        self.buf.write(name)
        self.buf.write(op)
        self.exp = self.buf.get()
    
    function set_local(name: str, op: str)
        self.buf.write(name)
        self.buf.write(op)
        self.buf.write(self.exp)
        self.exp = self.buf.get()
        self.exit_assign()
    
    function get_local(name: str)
        self.exp = name
    
    function call_function(name: str)
        self.buf.write(name)
        self.buf.write("(")
        self.write_args()
        self.buf.write(")")
        self.exit_call()
        self.exp = self.buf.get()
    
    function inc_global(name: str, op: str)
        self.buf.write(name)
        self.buf.write(op)
        self.exp = self.buf.get()
    
    function set_global(name: str, op: str)
        self.buf.write(name)
        self.buf.write(op)
        self.buf.write(self.exp)
        self.exp = self.buf.get()
        self.exit_assign()
    
    function get_global(name: str)
        self.buf.write(name)
        self.exp = self.buf.get()
    
    function self_exp()
        self.exp = "this"
    
    function sizeof_exp()
        self.buf.write(self.exp)
        self.buf.write(".length")
        self.exp = self.buf.get()
    
    function array_new(type: shark_type)
        self.buf.write("new ")
        var base_depth: int = 0
        var base_type: shark_type = cast(type, shark_array_type).type
        while instanceof(base_type, shark_array_type) do
            base_depth += 1
            base_type = cast(base_type, shark_array_type).type
        self.write_type(base_type)
        self.buf.write("[(int) (")
        self.buf.write(self.exp)
        self.buf.write(")]")
        for i to base_depth do
            self.buf.write("[0]")
        self.exp = self.buf.get()
    
    function object_new(type: shark_custom_type)
        self.buf.write("new ")
        self.buf.write(type.name)
        self.buf.write("(")
        self.write_args()
        self.exit_call()
        self.buf.write(")")
        self.exp = self.buf.get()
    
    function instanceof_exp(type: shark_custom_type)
        self.buf.write("(")
        self.buf.write(self.exp)
        self.buf.write(" instanceof ")
        self.buf.write(type.name)
        self.buf.write(")")
        self.exp = self.buf.get()
    
    function cast_exp(type: shark_type)
        self.buf.write("((")
        self.write_type(type)
        self.buf.write(") ")
        self.buf.write(self.exp)
        self.buf.write(")")
        self.exp = self.buf.get()
    
    function nested_exp()
        self.buf.write("(")
        self.buf.write(self.exp)
        self.buf.write(")")
        self.exp = self.buf.get()
    
    function null_exp()
        self.exp = "null"
    
    function true_exp()
        self.exp = "true"
    
    function false_exp()
        self.exp = "false"
    
    function int_exp(value: str)
        self.exp = value
    
    function float_exp(value: str)
        self.exp = value
    
    function char_exp(value: str)
        self.exp = value
    
    function str_exp(value: str)
        self.exp = value
